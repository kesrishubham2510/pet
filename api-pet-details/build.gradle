plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.4'
	id 'io.spring.dependency-management' version '1.1.3'
	// To publish the artifact
	id 'maven-publish'
	// To publish the project analysis on sonarqube
	id "org.sonarqube" version "4.4.1.3373"
}

ext.versions = [
		springBootVersion                : '3.1.4',
		springDependencyManagemnetVersion: '1.1.3',
		modelmapper                      : '3.1.1',
		springdocOpenAPI                 : '1.7.0',
		lombok                           : '1.18.26',
		appVersion                       : '0.0.3',
		group                            : 'com.myreflectionthoughts',
		archTestVersion                  : '1.1.0',
		testContainerVersion             : '1.19.0',
		actuatorVersion                  : '2.7.15',
		libraryPetVersion				 : '0.0.2',
		contextPropagationLibraryVersion : '1.0.6'
]

jar {
	enabled = true
}

group = "${versions.group}"
version = "${versions.appVersion}"

java {
	sourceCompatibility = '17'
}

repositories {
	maven {
		url project.property('nexusURL')
		allowInsecureProtocol true

		credentials{
			username property('nexusUsername')
			password property('nexusPassword')
		}
	}
	mavenCentral()

}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation "org.modelmapper:modelmapper:${versions.modelmapper}"
	implementation "org.springdoc:springdoc-openapi-webflux-core:${versions.springdocOpenAPI}"
	implementation "org.springdoc:springdoc-openapi-webflux-ui:${versions.springdocOpenAPI}"
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation "org.projectlombok:lombok:${versions.lombok}"
	compileOnly('org.projectlombok:lombok')
	annotationProcessor('org.projectlombok:lombok')
	testCompileOnly('org.projectlombok:lombok')
	testAnnotationProcessor('org.projectlombok:lombok')
	testImplementation "com.tngtech.archunit:archunit:${versions.archTestVersion}"
	testImplementation "org.testcontainers:mongodb:${versions.testContainerVersion}"
	// dependencies for observability
	implementation "org.springframework.boot:spring-boot-starter-actuator:${versions.actuatorVersion}"
	implementation "com.myreflectionthoughts:library-pet:${versions.libraryPetVersion}@jar"
	implementation "io.micrometer:context-propagation:${versions.contextPropagationLibraryVersion}"
}

tasks.named('test') {
	useJUnitPlatform()
}

publishing {
	publications {
		maven(MavenPublication) {
			group "${versions.group}"
			artifact("build/libs/api-pet-details-${versions.appVersion}.jar")
		}
	}
	repositories {
		maven {
			// to fetch the properties from the gradle.properties file
			name property('nexusRepoIdentifier')
			url  property('nexusURL')
			allowInsecureProtocol true
			credentials {
				username property('nexusUsername')
				password property('nexusPassword')
			}
		}
	}
}

tasks.named("publishMavenPublicationToNexus-localRepository") {
	dependsOn(tasks.named("jar"))
}

sonar{
	/*
        * during configuration phase, Project object has access to the properties
        * Accessing the properties using Project object
    */
	properties{
		property 'sonar.projectKey' , project.property('projectKey')
		property 'sonar.projectName' , project.property('projectName')
		property 'sonar.host.url' , project.property('sonarUrl')
		property 'sonar.token', project.property('projectToken')
	}
}

tasks.register('buildAndTagDockerImage', Exec) {
	commandLine 'docker', 'build',
			'-t', "${project.property('dockerHubUsername')}/${project.findProperty('projectName')}:${versions.appVersion}",
			'--build-arg', "projectIdentifier=${project.findProperty('projectName')}",
			'--build-arg', "artifactDirectory=build/libs/${project.property('projectName')}",
			'--build-arg', "artifactVersion=${versions.appVersion}",
			"/root/.jenkins/workspace/${project.findProperty('projectName')}/${project.findProperty('projectName')}/."

	workingDir '.'
	dependsOn(tasks.named('jar'))
}

tasks.register('dockerLogin',Exec) {
	commandLine 'docker', 'login',
			'-u', "${project.property('dockerHubUsername')}",
			'-p', "${System.getenv('DOCKER_ACCESS_TOKEN')}"

}

tasks.register('pushDockerImage',Exec){
	commandLine 'docker', 'push',
				"${project.property('dockerHubUsername')}/${project.property('projectName')}:${versions.appVersion}"

	dependsOn(tasks.named('dockerLogin'))
}

tasks.register('logoutDocker',Exec){
	commandLine 'docker', 'logout'
}